// 4D extension of Pcx by Hiroyuki Inou:
// Pcx - Point cloud importer & renderer for Unity
// https://github.com/keijiro/Pcx

#include "UnityCG.cginc"
#include "Common.cginc"

// Uniforms
half4 _Tint;
float _PointSize;
float4x4 _Transform;
float _Chiral;

float4x4 _Rotation4D, _Tilt4D_LeftEye, _Tilt4D_RightEye;
float4 _Translation4D;

// Vertex input attributes
struct Attributes
{
#if _COMPUTE_BUFFER
    uint vertexID : SV_VertexID;
#else
    float4 position : POSITION;
    half3 color : COLOR;
    float2 uv2 : TEXCOORD1;
#endif
};

// Fragment varyings
struct Varyings
{
    float4 position : SV_POSITION;
#if !PCX_SHADOW_CASTER
    half3 color : COLOR;
    UNITY_FOG_COORDS(0)
#endif
};


#if _COMPUTE_BUFFER
struct Point4D
{
	float4 position;
	uint color;
};

StructuredBuffer<Point4D> _PointBuffer;
#endif

// Vertex phase
Varyings Vertex(Attributes input)
{
    // Retrieve vertex attributes.
#if _COMPUTE_BUFFER
	Point4D pt = _PointBuffer[input.vertexID];
	float4 pos4d = mul(_Rotation4D, pt.position) + _Translation4D;
	//float4 pos4d = pt.position;
	float4 pos = mul(_Transform, float4(pos4d.xyz, 1));
	half3 col = PcxDecodeColor(pt.color);
#else
    float4 pos4d = float4(input.position.xyz, _Chiral ? -input.uv2.x : input.uv2.x);
    pos4d = mul(_Rotation4D, pos4d) + _Translation4D;
    //pos4d = pos4d + _Translation4D;
    float4 pos = float4(pos4d.xyz, 1);
    half3 col = input.color;
#endif

#if !PCX_SHADOW_CASTER
    // Color space convertion & applying tint
    #if UNITY_COLORSPACE_GAMMA
        col *= _Tint.rgb * 2;
    #else
        col *= LinearToGammaSpace(_Tint.rgb) * 2;
        col = GammaToLinearSpace(col);
    #endif
#endif

    // Set vertex output.
    Varyings o;
    //o.position = UnityObjectToClipPos(pos);
	float4 pos2 = float4(UnityObjectToViewPos(pos).xyz,pos4d.w);
	if (unity_StereoEyeIndex == 0) {
		o.position = mul(UNITY_MATRIX_P, float4(mul(_Tilt4D_LeftEye,pos2).xyz,1));
	} else {
		o.position = mul(UNITY_MATRIX_P, float4(mul(_Tilt4D_RightEye,pos2).xyz,1));
	}
#if !PCX_SHADOW_CASTER
    o.color = col;
    UNITY_TRANSFER_FOG(o, o.position);
#endif
    return o;
}

// Geometry phase
[maxvertexcount(36)]
void Geometry(point Varyings input[1], inout TriangleStream<Varyings> outStream)
{
    float4 origin = input[0].position;
    float2 extent = abs(UNITY_MATRIX_P._11_22 * _PointSize);

    // Copy the basic information.
    Varyings o = input[0];

    // Determine the number of slices based on the radius of the
    // point on the screen.
    float radius = extent.y / origin.w * _ScreenParams.y;
    uint slices = min((radius + 1) / 5, 4) + 2;

    // Slightly enlarge quad points to compensate area reduction.
    // Hopefully this line would be complied without branch.
    if (slices == 2) extent *= 1.2;

    // Top vertex
    o.position.y = origin.y + extent.y;
    o.position.xzw = origin.xzw;
    outStream.Append(o);

    UNITY_LOOP for (uint i = 1; i < slices; i++)
    {
        float sn, cs;
        sincos(UNITY_PI / slices * i, sn, cs);

        // Right side vertex
        o.position.xy = origin.xy + extent * float2(sn, cs);
        outStream.Append(o);

        // Left side vertex
        o.position.x = origin.x - extent.x * sn;
        outStream.Append(o);
    }

    // Bottom vertex
    o.position.x = origin.x;
    o.position.y = origin.y - extent.y;
    outStream.Append(o);

    outStream.RestartStrip();
}

half4 Fragment(Varyings input) : SV_Target
{
#if PCX_SHADOW_CASTER
    return 0;
#else
    half4 c = half4(input.color, _Tint.a);
    UNITY_APPLY_FOG(input.fogCoord, c);
    return c;
#endif
}

